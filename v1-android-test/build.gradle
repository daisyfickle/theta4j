/*
 * Copyright (C) 2015 theta4j project
 */

import org.theta4j.android.Version

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
    }
}

repositories {
    google()
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDir "${project(':v1').projectDir}/src/main/java"
        }

        test {
            java.srcDir "${project(':v1').projectDir}/src/test/java"
        }

        androidTest {
            java.srcDir "${project(':v1').projectDir}/src/integrationTest/java"
        }
    }

    defaultConfig {
        def version = Version.parse(rootProject.version)

        minSdkVersion 19
        targetSdkVersion 28
        versionCode version.code
        versionName version.name

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.3.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'ch.qos.logback:logback-classic:1.1.11'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    androidTestImplementation 'com.github.tony19:logback-android-core:1.1.1-2'
    androidTestImplementation 'com.github.tony19:logback-android-classic:1.1.1-2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
}
